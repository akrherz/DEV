load "$WEBWXHOME/ncl_lib/functions.ncl"
load "$WEBWXHOME/ncl_lib/cmap.ncl"
load "$WEBWXHOME/ncl_lib/meteo_funcs.ncl"
;
;# File eta_model.ncl -----------------------------------------------------
;# 
;# Purpose: Create plots of wind speed and temperature
;#          on pressure levels from Eta model input files.
;#
;# Input:   Eta 211 (80-km) GRIB file
;#
;# Output:  NCAR Graphics output file (gmeta) containing plots of:
;#             - wind speed
;#             - temperatures
;#             - 500mb vorticity
;#             - 500/700/850/925 RH
;#             - 500/700 vertical velocity + 850/925 mixing ratio
;#             - various surface variables
;#
;# Author:  Greg Thompson  25 Apr 2002
;#
;#--------------------------------------------------------------------------
;
begin
;
    ; Create list of pressure levels for plotting.

    nheights=6
    plot_heights_mb=(/200,250,300,500,700,850/)

    ; Create our own colormap using included function.

    cmap=gen_colormap()

    ; Open a workstation, ncgm, or postscript file

    gmetafile = "gmeta_ETA12"
    wks = xwk("ncgm",gmetafile)
    setvalues wks
        "wkColorMap" : cmap
    end setvalues
 
    ; Ingest GRIB file for Eta data (GRIB filename dynamically changed).

    gribfile = "/old_ftp/root/pub/grib/Eta_80/2002Apr25/200211500.F12.ETA80.grb"
    print ("Ingesting GRIB file, " + gribfile)
    file1 = addfile (gribfile,"r") 

    ; Set flag for plotting analysis (0) vs. forecast time (1)

    precip_flag = new(1, logical)
    anal_fcst = new(1, logical)
    anal_fcst = 1
    if (anal_fcst .ne. 0) then
       precip_flag = 1
    else
       precip_flag = 0
    end if

    ; Get terrain from external file
    ; This terrain file can be regenerated by gen_topo_file.ncl if needed

    terrainfile = "$WEBWXHOME/eta/scripts/eta_topo.nc"
    tfile=addfile(terrainfile, "r")
    terr_field=tfile->height

    ; Create date/time plus model cycle strings (dynamically changed).

    date_string = "12 hour forecast valid 1200 UTC Thu 25 Apr 2002"
    level_string = "Eta (00z 25 Apr)"

    date_time = create "date_time" textItemClass wks
        "txString" : date_string
        "txFont" : "helvetica"
        "txFontColor" : 1
        "txJust" : "BOTTOMLEFT"
        "txFontHeightF" : 0.018
    end create
    level_anno = create "level_anno" textItemClass wks
        "txString" : level_string
        "txFont" : "helvetica"
        "txFontColor" : 1
        "txJust" : "BOTTOMRIGHT"
        "txFontHeightF" : 0.014
    end create

    ; Setup Map characteristics based on GRIB ruc40 grid

    mp = gribmap(wks, "211")
    setvalues mp
        "vpXF": 0.01
        "vpYF": 0.99
        "vpWidthF": 0.98
        "vpHeightF": 0.98
        "mpFillOn" : True
        "mpLandFillColor" : 46
        "mpOceanFillColor" : 47
        "mpInlandWaterFillColor" : 47
        "mpGeophysicalLineColor" : 35
        "mpUSStateLineColor" : 35
        "mpNationalLineColor" : 35
    end setvalues

    ; Actual plotted window differs from requested because of map aspect ratio.

    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues

    setvalues date_time
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    ; Loop thru levels to plot and create wind speed, then streamlines, then
    ; temperature contours.

    do i=0, nheights-1

        pLevel   = plot_heights_mb(i)
        print ("Plotting at level: " + pLevel)

        titleStringTemp = "" + pLevel + " mb Heights (dm) / Temperature (:S:o:N:C)"
        titleStringWind = "" + pLevel + " mb Heights (dm) / Isotachs (knots)"
        zResource = "geoz" + pLevel
        z2d      = file1->HGT_211_ISBL({pLevel},:,:)*.1
        t2d      = file1->TMP_211_ISBL({pLevel},:,:)-273.15
        u2d      = file1->U_GRD_211_ISBL({pLevel},:,:)
        v2d      = file1->V_GRD_211_ISBL({pLevel},:,:)
        speed    = sqrt(u2d*u2d + v2d*v2d)*1.94
        wind_vec = vector(wks,MakeData((/u2d, v2d/), "vect2"),"vect2")

        cn_wspd  = contour(wks,MakeData(speed,"speed"),"speed")
        cn_temp  = contour(wks,MakeData(t2d, "tempC"),"tempC")
        cn_temp2 = contour(wks,MakeData(t2d, "tempC"),"tempC")
        cn_hght  = contour(wks,MakeData(z2d, zResource),zResource)

        setvalues cn_hght
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "pmLabelBarDisplayMode" : "NEVER"
        end setvalues
        setvalues cn_temp
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : titleStringTemp
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "NEVER"
        end setvalues
        setvalues cn_temp2
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainOn" : False
            "cnMinLevelValF" : -62.5
            "cnMaxLevelValF" : 37.5
            "cnLineLabelsOn" : False
            "cnMonoLineThickness" : True
            "cnLineThicknessF" : 1.0
            "cnLineDashPatterns" : (/ 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 0, 0, 0, 0, 0, 0, 0, 0/)
            "pmLabelBarDisplayMode" : "NEVER"
        end setvalues
        setvalues cn_wspd
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : titleStringWind
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF" : 0.05
            "pmLabelBarWidthF" : vpw*.75
            "pmLabelBarParallelPosF" : 1.0
            "pmLabelBarOrthogonalPosF" : 0.0
            "lbLabelsOn" : "True"
            "lbLabelFontHeightF" : 0.010
            "lbLabelStride" : 1
            "lbTitleOn" : True
            "lbTitleString" : "(knots)"
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleFontHeightF" : 0.016
        end setvalues
        if (pLevel .ge. 700) then
            setvalues cn_wspd
                "cnLevelSelectionMode" : "ExplicitLevels"
                "cnLevels"             : (/15.,20.,30.,40.,50.,60.,80.,100./)
                "cnLineLabelsOn"       : False
                "cnLinesOn"            : False
                "cnFillOn"             : True
                "cnFillColors"         : (/-1,37,38,39,40,41,42,43,44/)
            end setvalues
        else
            setvalues cn_wspd
                "cnLevelSelectionMode" : "ExplicitLevels"
                "cnLevels"             : (/30.,40.,50.,60.,80.,100.,125.,150./)
                "cnLineLabelsOn"       : False
                "cnLinesOn"            : False
                "cnFillOn"             : True
                "cnFillColors"         : (/-1,39,40,41,42,43,44,28,2/)
            end setvalues
        end if

        ; Generate terrain contours for terrain above this flight level
        ; Skip this if the the flight level is above the max terrain

        terrain_is_below = True
        height = std_atmos(pLevel*100.0)
        max_terr=max(terr_field)
        if (height .gt. max_terr) then
            terrain_is_below = False
        end if
        if (terrain_is_below .eq. True) then
            cn_terrain=contour(wks,MakeData(terr_field,"null"),"null")
            setvalues cn_terrain
                "vpXF"                  : vpx
                "vpYF"                  : vpy
                "vpWidthF"              : vpw
                "vpHeightF"             : vph
                "cnLevelSelectionMode"  : "ExplicitLevels"
                "cnLevels"              : height
                "cnLinesOn"             : True
                "cnLineLabelsOn"        : False
                "cnFillOn"              : True
                "cnFillColors"          : (/-1,48,-1/)
                "cnMonoFillPattern"     : False
                "cnFillPatterns"        : (/0,4,0/)
                "pmLabelBarDisplayMode" : "NEVER"
            end setvalues
        end if

        ;------------------------------- Winds plots --------------------------#

        print ("      plotting winds")

        setvalues mp
            "mpFillOn" : True
        end setvalues
        draw(mp)

        overlay(cn_wspd, wind_vec)
        draw((/cn_wspd, cn_hght/))

        if (terrain_is_below .eq. True) then
            draw(cn_terrain)
        end if

        setvalues mp
            "mpFillOn" : False
        end setvalues
        draw(mp)

        draw((/date_time,level_anno/))

        frame(wks)
        print ("      .............. done")

        ;------------------------------- Temperature plots --------------------#

        print ("      plotting temperature")

        setvalues mp
            "mpFillOn" : True
        end setvalues
        draw(mp)

        draw((/cn_hght,cn_temp2,cn_temp/))

        if (terrain_is_below .eq. True) then
            draw(cn_terrain)
        end if

        setvalues mp
            "mpFillOn" : False
        end setvalues
        draw(mp)

        draw((/date_time,level_anno/))

        print ("      .................... done")

        frame(wks)


        ;------------------------------- Clean-up -----------------------------#

        if (pLevel .eq. 500) then
            cn_hght500 = cn_hght
        end if
        delete (cn_wspd)
        delete (cn_temp)
        delete (cn_hght)
        delete (wind_vec)

    end do

    ;------------------------------- 500 mb vorticity plot --------------------#

    print ("Plotting 500 mb vorticity")
    cn_vort = contour(wks,MakeData(file1->ABS_V_211_ISBL({500},:,:)*100000.0,"vort"),"vort")
    setvalues cn_vort
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : "500 mb Heights (dm) / Abs. Vorticity (x10:S:-5:N: s:S:-1:N:)"
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF" : 0.05
            "pmLabelBarWidthF" : vpw*.75
            "pmLabelBarParallelPosF" : 1.0
            "pmLabelBarOrthogonalPosF" : 0.0
            "lbLabelsOn" : "True"
            "lbLabelFontHeightF" : 0.010
            "lbLabelStride" : 1
            "lbTitleOn" : True
            "lbTitleString" : "(x10:S:-5:N: s:S:-1:N:)"
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleFontHeightF" : 0.016
    end setvalues

    setvalues mp
        "mpFillOn" : True
    end setvalues

    draw((/mp,cn_vort,cn_hght500/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)

    draw((/date_time,level_anno/))

    frame(wks)

    ;------------------------------- 4-panel T / RH plot ----------------------#

    print ("Plotting 4-panel T/RH chart")

    tmp500 = file1->TMP_211_ISBL({500},:,:)
    tmp700 = file1->TMP_211_ISBL({700},:,:)
    tmp850 = file1->TMP_211_ISBL({850},:,:)
    tmp925 = file1->TMP_211_ISBL({925},:,:)

    rh500   = file1->R_H_211_ISBL({500},:,:)*0.01
    rh700   = file1->R_H_211_ISBL({700},:,:)*0.01
    rh850   = file1->R_H_211_ISBL({850},:,:)*0.01
    rh925   = file1->R_H_211_ISBL({925},:,:)*0.01

    rh500   = rh500 > 0.01
    rh500   = rh500 < 0.999
    rh700   = rh700 > 0.01
    rh700   = rh700 < 0.999
    rh850   = rh850 > 0.01
    rh850   = rh850 < 0.999
    rh925   = rh925 > 0.01
    rh925   = rh925 < 0.999

    cn500_T = contour(wks,MakeData(tmp500-273.15,"tempC"),"tempC")
    cn700_T = contour(wks,MakeData(tmp700-273.15,"tempC"),"tempC")
    cn850_T = contour(wks,MakeData(tmp850-273.15,"tempC"),"tempC")
    cn925_T = contour(wks,MakeData(tmp925-273.15,"tempC"),"tempC")

    cn500_R = contour(wks,MakeData(rh500*100.0,"relh"),"relh")
    cn700_R = contour(wks,MakeData(rh700*100.0,"relh"),"relh")
    cn850_R = contour(wks,MakeData(rh850*100.0,"relh"),"relh")
    cn925_R = contour(wks,MakeData(rh925*100.0,"relh"),"relh")

    ;---------------------------- 500 mb  T/RH Plot -(upper-left)--------------#

    titleStringRH = "500 mb Humidty (%) / Temperature (:S:o:N:C)"
    setvalues mp
        "vpXF" : 0.01
        "vpYF" : 0.99
        "vpWidthF" : 0.48
        "vpHeightF" : 0.48
        "mpFillOn" : True
    end setvalues
    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues
    setvalues cn500_T
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainOn" : "False"
        "pmLabelBarDisplayMode" : "NEVER"
        "cnMonoLineThickness" : True
        "cnLineThicknessF" : 2.0
        "cnLineLabelFontHeightF" : 0.009
    end setvalues
    setvalues cn500_R
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainString" : titleStringRH
        "tiMainFontHeightF" : 0.018
        "pmLabelBarHeightF" : 0.05
        "pmLabelBarWidthF" : vpw*.75
        "pmLabelBarParallelPosF" : 1.0
        "pmLabelBarOrthogonalPosF" : 0.0
        "lbLabelsOn" : "True"
        "lbLabelFontHeightF" : 0.009
        "lbLabelStride" : 1
        "lbTitleOn" : True
        "lbTitleString" : "(percent)"
        "lbTitlePosition" : "Bottom"
        "lbTitleJust" : "BottomRight"
        "lbTitleFontHeightF" : 0.009
    end setvalues
    setvalues date_time
        "txFontHeightF" : 0.011
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txFontHeightF" : 0.010
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    draw((/mp,cn500_R,cn500_T/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    ;---------------------------- 700 mb  T/RH Plot -(upper-right)-------------#

    titleStringRH = "700 mb Humidty (%) / Temperature (:S:o:N:C)"
    setvalues mp
        "vpXF" : vpx+0.5
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "mpFillOn" : True
    end setvalues
    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues
    setvalues cn700_T
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainOn" : "False"
        "pmLabelBarDisplayMode" : "NEVER"
        "cnMonoLineThickness" : True
        "cnLineThicknessF" : 2.0
        "cnLineLabelFontHeightF" : 0.009
    end setvalues
    setvalues cn700_R
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainString" : titleStringRH
        "tiMainFontHeightF" : 0.018
        "pmLabelBarHeightF" : 0.05
        "pmLabelBarWidthF" : vpw*.75
        "pmLabelBarParallelPosF" : 1.0
        "pmLabelBarOrthogonalPosF" : 0.0
        "lbLabelsOn" : "True"
        "lbLabelFontHeightF" : 0.009
        "lbLabelStride" : 1
        "lbTitleOn" : True
        "lbTitleString" : "(percent)"
        "lbTitlePosition" : "Bottom"
        "lbTitleJust" : "BottomRight"
        "lbTitleFontHeightF" : 0.009
    end setvalues
    setvalues date_time
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    draw((/mp,cn700_R,cn700_T/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    ;---------------------------- 850 mb  T/RH Plot -(lower-left)--------------#

    titleStringRH = "850 mb Humidty (%) / Temperature (:S:o:N:C)"
    setvalues mp
        "vpXF" : vpx-0.5
        "vpYF" : vpy-0.5
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "mpFillOn" : True
    end setvalues
    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues
    setvalues cn850_T
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainOn" : "False"
        "pmLabelBarDisplayMode" : "NEVER"
        "cnMonoLineThickness" : True
        "cnLineThicknessF" : 2.0
        "cnLineLabelFontHeightF" : 0.009
    end setvalues
    setvalues cn850_R
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainString" : titleStringRH
        "tiMainFontHeightF" : 0.018
        "pmLabelBarHeightF" : 0.05
        "pmLabelBarWidthF" : vpw*.75
        "pmLabelBarParallelPosF" : 1.0
        "pmLabelBarOrthogonalPosF" : 0.0
        "lbLabelsOn" : "True"
        "lbLabelFontHeightF" : 0.009
        "lbLabelStride" : 1
        "lbTitleOn" : True
        "lbTitleString" : "(percent)"
        "lbTitlePosition" : "Bottom"
        "lbTitleJust" : "BottomRight"
        "lbTitleFontHeightF" : 0.009
    end setvalues
    setvalues date_time
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    draw((/mp,cn850_R,cn850_T/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    ;---------------------------- 925 mb  T/RH Plot -(lower-right)-------------#

    titleStringRH = "925 mb Humidty (%) / Temperature (:S:o:N:C)"
    setvalues mp
        "vpXF" : vpx+0.5
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "mpFillOn" : True
    end setvalues
    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues
    setvalues cn925_T
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainOn" : "False"
        "pmLabelBarDisplayMode" : "NEVER"
        "cnMonoLineThickness" : True
        "cnLineThicknessF" : 2.0
        "cnLineLabelFontHeightF" : 0.009
    end setvalues
    setvalues cn925_R
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainString" : titleStringRH
        "tiMainFontHeightF" : 0.018
        "pmLabelBarHeightF" : 0.05
        "pmLabelBarWidthF" : vpw*.75
        "pmLabelBarParallelPosF" : 1.0
        "pmLabelBarOrthogonalPosF" : 0.0
        "lbLabelsOn" : "True"
        "lbLabelFontHeightF" : 0.009
        "lbLabelStride" : 1
        "lbTitleOn" : True
        "lbTitleString" : "(percent)"
        "lbTitlePosition" : "Bottom"
        "lbTitleJust" : "BottomRight"
        "lbTitleFontHeightF" : 0.009
    end setvalues
    setvalues date_time
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    draw((/mp,cn925_R,cn925_T/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    frame(wks)


    ;------------------------------- 4-panel Q / VV plot ----------------------#

    print ("Plotting 4-panel W/Q contour plot")

    r_850   = 0.622*(rh850*e_sub_s(tmp850))/(85000.0-rh850*e_sub_s(tmp850))
    r_925   = 0.622*(rh925*e_sub_s(tmp925))/(92500.0-rh925*e_sub_s(tmp925))
    cn850_r = contour(wks,MakeData(r_850*1000.,"Mixr"),"Mixr")
    cn925_r = contour(wks,MakeData(r_925*1000.,"Mixr"),"Mixr")

    rho = 50000./(287.05*tmp500)
    vv500 = 100.*file1->V_VEL_211_ISBL({500},:,:)*(-1./(rho(:,:)*9.8))
    rho = 70000./(287.05*tmp700)
    vv700 = 100.*file1->V_VEL_211_ISBL({700},:,:)*(-1./(rho(:,:)*9.8))

    cn500_VV  = contour(wks,MakeData(vv500,"Vvel"),"Vvel")
    cn700_VV  = contour(wks,MakeData(vv700,"Vvel"),"Vvel")

    ;---------------------------- 500 mb VV  Plot -(upper-left)----------------#

    titleStringVV = "500 mb Vertical Motion (cm/s)"

    setvalues mp
        "vpXF": 0.01
        "vpYF": 0.99
        "vpWidthF": 0.48
        "vpHeightF": 0.48
        "mpFillOn" : True
    end setvalues
    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues
    setvalues cn500_VV
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainString" : titleStringVV
        "tiMainFontHeightF" : 0.018
        "pmLabelBarHeightF" : 0.05
        "pmLabelBarWidthF" : vpw*.75
        "pmLabelBarParallelPosF" : 1.0
        "pmLabelBarOrthogonalPosF" : 0.0
        "lbLabelFontHeightF" : 0.009
        "lbLabelsOn" : "True"
        "lbTitleOn" : True
        "lbTitleString" : "(cm s:S:-1:N:)"
        "lbTitlePosition" : "Bottom"
        "lbTitleJust" : "BottomRight"
        "lbTitleFontHeightF" : 0.009
    end setvalues
    setvalues date_time
        "txFontHeightF" : 0.011
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txFontHeightF" : 0.010
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    draw((/mp,cn500_VV/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    ;---------------------------- 700 mb VV  Plot -(upper-right)---------------#

    titleStringVV = "700 mb Vertical Motion (cm/s)"

    setvalues mp
        "vpXF": vpx+0.5
        "vpYF": vpy
        "vpWidthF": vpw
        "vpHeightF": vph
        "mpFillOn" : True
    end setvalues
    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues
    setvalues cn700_VV
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainString" : titleStringVV
        "tiMainFontHeightF" : 0.018
        "pmLabelBarHeightF" : 0.05
        "pmLabelBarWidthF" : vpw*.75
        "pmLabelBarParallelPosF" : 1.0
        "pmLabelBarOrthogonalPosF" : 0.0
        "lbLabelFontHeightF" : 0.009
        "lbLabelsOn" : "True"
        "lbTitleOn" : True
        "lbTitleString" : "(cm s:S:-1:N:)"
        "lbTitlePosition" : "Bottom"
        "lbTitleJust" : "BottomRight"
        "lbTitleFontHeightF" : 0.009
    end setvalues
    setvalues date_time
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    draw((/mp,cn700_VV/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    ;---------------------------- 850 mb Q Plot -(lower-left)------------------#

    titleStringQ = "850 mb Mixing Ratio (g/kg)"

    setvalues mp
        "vpXF": vpx-0.5
        "vpYF": vpy-0.5
        "vpWidthF": vpw
        "vpHeightF": vph
        "mpFillOn" : True
    end setvalues
    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues
    setvalues cn850_r
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainString" : titleStringQ
        "tiMainFontHeightF" : 0.018
        "pmLabelBarHeightF" : 0.05
        "pmLabelBarWidthF" : vpw*.75
        "pmLabelBarParallelPosF" : 1.0
        "pmLabelBarOrthogonalPosF" : 0.0
        "lbLabelFontHeightF" : 0.009
        "lbLabelsOn" : "True"
        "lbTitleOn" : True
        "lbTitleString" : "(g kg:S:-1:N:)"
        "lbTitlePosition" : "Bottom"
        "lbTitleJust" : "BottomRight"
        "lbTitleFontHeightF" : 0.009
    end setvalues
    setvalues date_time
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    draw((/mp,cn850_r/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    ;---------------------------- 925 mb Q Plot -(lower-right)-----------------#

    titleStringQ = "925 mb Mixing Ratio (g/kg)"

    setvalues mp
        "vpXF": vpx+0.5
        "vpYF": vpy
        "vpWidthF": vpw
        "vpHeightF": vph
        "mpFillOn" : True
    end setvalues
    getvalues mp
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
    end getvalues
    setvalues cn925_r
        "vpXF" : vpx
        "vpYF" : vpy
        "vpWidthF" : vpw
        "vpHeightF" : vph
        "tiMainString" : titleStringQ
        "tiMainFontHeightF" : 0.018
        "pmLabelBarHeightF" : 0.05
        "pmLabelBarWidthF" : vpw*.75
        "pmLabelBarParallelPosF" : 1.0
        "pmLabelBarOrthogonalPosF" : 0.0
        "lbLabelFontHeightF" : 0.009
        "lbLabelsOn" : "True"
        "lbTitleOn" : True
        "lbTitleString" : "(g kg:S:-1:N:)"
        "lbTitlePosition" : "Bottom"
        "lbTitleJust" : "BottomRight"
        "lbTitleFontHeightF" : 0.009
    end setvalues
    setvalues date_time
        "txPosXF" : vpx
        "txPosYF" : vpy + 0.005
    end setvalues
    setvalues level_anno
        "txPosXF" : vpx+vpw
        "txPosYF" : vpy + 0.005
    end setvalues

    draw((/mp,cn925_r/))

    setvalues mp
        "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    frame(wks)


    ;------------------------------- SURFACE plotting -------------------------#

    ; Initialize some strings for annotation
    temp_string = "Surface (2m) Temperature (:S:o:N:F)"
    dewp_string = "Surface (2m) Dewpoint Temperature (:S:o:N:F)"
    thet_string = "Surface (2m) Theta-E (:S:o:N:C)"
    mslp_string = "Sea-Level Pressure (mb)"
    wind_string = "Surface (10m) Wind Speed (knots) / MSLP (mb)"
    pwat_string = "Precipitable Water (cm) / Lifted Index (:S:o:N:C)"
    cape_string = "CAPE / CIN (J kg:S:-1:N:)"
    sreh_string = "Storm-relative Helicity (m:S:2:N: s:S:-2:N:)"
    prcp_string = "Precip (in) (total-shaded; convective-dashed)"

    tsfc    = file1->TMP_211_HTGL
    rsfc    = file1->R_H_211_HTGL*0.01
    psfc    = file1->PRES_211_SFC
    ; Set RH minimum to 0.0 and maximum to 1.0 since GRIB compression can introduce
    ; values outside this range.
    rsfc    = rsfc > 0.01
    rsfc    = rsfc < 0.999
    es_td   = rsfc*e_sub_s(tsfc)
    mixrat  = 0.622*es_td/(psfc-es_td)
    DewpK   = t_dew(psfc,mixrat)
    t_lclK  = t_lcl(tsfc,DewpK)
    ThetaE  = theta_e(psfc, tsfc, mixrat, t_lclK)
    usfc = file1->U_GRD_211_HTGL
    vsfc = file1->V_GRD_211_HTGL
    speed = sqrt(usfc^2 + vsfc^2)*1.943844
    cn_wind = contour(wks,MakeData(speed,"null"),"null")
    vcsfc   = vector(wks,MakeData((/usfc, vsfc/),"vect2"),"vect2")

    ; Create data contours for interested fields
    cn_temp = contour(wks,MakeData( (tsfc - 273.15)*9.0/5.0 + 32,"tempF"),"tempF")
    cn_dewp = contour(wks,MakeData( (DewpK - 273.15)*9.0/5.0 + 32,"dewpF"),"dewpF")
    cn_mslp = contour(wks,MakeData(file1->MSLET_211_MSL*0.01,"mslp"),"mslp")
    cn_thck = contour(wks,MakeData((file1->HGT_211_ISBL({500},:,:)-file1->HGT_211_ISBL({1000},:,:))/10.0,"thick"),"thick")
    cn_cape = contour(wks,MakeData(file1->CAPE_211_SFC,"cape"),"cape")
    cn_cin  = contour(wks,MakeData(file1->CIN_211_SFC,"cin"),"cin")
    cn_sreh = contour(wks,MakeData(file1->HLCY_211_HTGY,"sreh"),"sreh")
    cn_lift = contour(wks,MakeData(file1->PLI_211_SPDY,"lift"),"lift")
    cn_pwat = contour(wks,MakeData(file1->P_WAT_211_EATM*0.1,"pwat"),"pwat")
    cn_thet = contour(wks,MakeData(ThetaE-273.15,"thetae"),"thetae")

    if (precip_flag) then
        precipt = file1->A_PCP_211_SFC_acc/25.4
        precip1 = file1->ACPCP_211_SFC_acc/25.4
        cn_pcpt = contour(wks,MakeData(precipt,"totpcp"),"totpcp")
        cn_pcp1 = contour(wks,MakeData(precipt-precip1,"totpcp"),"totpcp")
        cn_pcp2 = contour(wks,MakeData(precip1,"totpcp"),"totpcp")
    end if

;---------------------------- Reset Map params --------------------------

    setvalues mp
            "vpXF": 0.01
            "vpYF": 0.99
            "vpWidthF": 0.98
            "vpHeightF": 0.98
    end setvalues
    getvalues mp
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
    end getvalues
    setvalues date_time
            "txPosXF"       : vpx
            "txPosYF"       : vpy+0.005
            "txJust" : "BOTTOMLEFT"
            "txFontHeightF" : 0.018
    end setvalues
    setvalues level_anno
            "txPosXF" : vpx+vpw
            "txPosYF" : vpy + 0.005
            "txFontHeightF" : 0.014
    end setvalues

    ;---------------------------- Surface Temperature Plot -----------------

    print ("Plotting SFC Temperature contours")
    setvalues cn_temp
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : temp_string
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF"            : 0.05
            "pmLabelBarWidthF"             : vpw*0.75
            "pmLabelBarParallelPosF"       : 0.995
            "pmLabelBarOrthogonalPosF"     : 0.0
            "lbLabelsOn" : True
            "lbLabelStride" : 2
            "lbLabelFontHeightF" : 0.014
            "lbTitleOn" : True
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleString" : ":S:o:N:F"
            "lbTitleFontHeightF" : 0.016
    end setvalues
    draw((/cn_temp,mp/))
    draw((/date_time,level_anno/))

    frame(wks)

    ;---------------------------- Surface Dewpoint Plot --------------------

    print ("Plotting SFC Dewpoint contours")
    setvalues cn_dewp
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : dewp_string
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF"            : 0.05
            "pmLabelBarWidthF"             : vpw*0.75
            "pmLabelBarParallelPosF"       : 0.995
            "pmLabelBarOrthogonalPosF"     : 0.0
            "lbLabelsOn" : "True"
            "lbLabelStride" : 2
            "lbLabelFontHeightF" : 0.014
            "lbTitleOn" : True
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleString" : ":S:o:N:F"
            "lbTitleFontHeightF" : 0.016
    end setvalues
    draw((/cn_dewp,mp/))
    draw((/date_time,level_anno/))

    frame(wks)

    ;---------------------------- MSLP / Thickness / Sfc Wind speed --------

    print ("Plotting MSLP/Thickness plus sfc wind speed contours")
    setvalues mp
            "mpFillOn" : True
    end setvalues
    setvalues cn_wind
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "cnLevelSelectionMode" : "ExplicitLevels"
            "cnLevels"             : (/15.,20.,30.,40.,50.,60.,80.,100./)
            "cnHighLabelsOn"       : False
            "cnLowLabelsOn"        : False
            "cnLineLabelsOn"       : False
            "cnLinesOn"            : False
            "cnFillOn"             : True
            "cnFillColors"         : (/-1,37,38,39,40,41,42,43,44/)
            "tiMainString" : wind_string
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF"            : 0.05
            "pmLabelBarWidthF"             : vpw*0.75
            "pmLabelBarParallelPosF"       : 0.995
            "pmLabelBarOrthogonalPosF"     : 0.0
            "lbLabelsOn" : "True"
            "lbLabelStride" : 1
            "lbLabelFontHeightF" : 0.014
            "lbTitleOn" : True
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleString" : "knots"
            "lbTitleFontHeightF" : 0.016
    end setvalues
    setvalues cn_mslp
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainOn" : "False"
            "cnFillOn" : "False"
            "cnLevelSpacingF" : 4.0
            "cnLineThicknessF" : 2.0
            "pmLabelBarDisplayMode" : "NEVER"
    end setvalues
    setvalues cn_thck
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainOn" : "False"
            "pmLabelBarDisplayMode" : "NEVER"
    end setvalues
    overlay(cn_wind,vcsfc)
    draw((/mp,cn_wind,cn_mslp,cn_thck/))
    setvalues mp
            "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    frame(wks)

    ;---------------------------- CAPE / CIN -------------------------------

    print ("Plotting CAPE/CIN contours")
    setvalues mp
            "mpFillOn" : True
    end setvalues
    setvalues cn_cape
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : cape_string
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF"            : 0.05
            "pmLabelBarWidthF"             : vpw*.65
            "pmLabelBarParallelPosF"       : 0.995
            "pmLabelBarOrthogonalPosF"     : 0.0
            "lbLabelsOn" : "True"
            "lbLabelFontHeightF" : 0.014
            "lbLabelStride" : 2
            "lbTitleOn" : True
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleString" : "J kg:S:-1:N:"
            "lbTitleFontHeightF" : 0.016
    end setvalues
    setvalues cn_cin
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF"            : 0.05
            "pmLabelBarWidthF"             : vpw*.30
            "pmLabelBarParallelPosF"       : 0.35
            "pmLabelBarOrthogonalPosF"     : 0.0
            "lbLabelsOn" : "True"
            "lbLabelFontHeightF" : 0.014
            "lbLabelStride" : 1
            "lbBoxLinesOn" : True
            "lbTitleOn" : False
    end setvalues

    draw((/mp,cn_cape,cn_cin/))
    setvalues mp
            "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    frame(wks)

    ;---------------------------- Precip-Water / LI Plot -------------------

    print ("Plotting Precip-Water and LI contours")
    setvalues mp
            "mpFillOn" : True
    end setvalues
    setvalues cn_lift
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "pmLabelBarDisplayMode" : "NEVER"
    end setvalues
    setvalues cn_pwat
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : pwat_string
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF"            : 0.05
            "pmLabelBarWidthF"             : vpw*0.75
            "pmLabelBarParallelPosF"       : 0.995
            "pmLabelBarOrthogonalPosF"     : 0.0
            "lbLabelsOn" : "True"
            "lbLabelFontHeightF" : 0.014
            "lbTitleOn" : True
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleString" : "cm"
            "lbTitleFontHeightF" : 0.016
    end setvalues

    draw((/mp, cn_pwat,cn_lift/))
    setvalues mp
            "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    frame(wks)

    ;---------------------------- SREH Plot --------------------------------

    print ("Plotting SREH contours")
    setvalues mp
            "mpFillOn" : True
    end setvalues
    setvalues cn_sreh
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : sreh_string
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF"            : 0.05
            "pmLabelBarWidthF"             : vpw*0.75
            "pmLabelBarParallelPosF"       : 0.995
            "pmLabelBarOrthogonalPosF"     : 0.0
            "lbLabelsOn" : "True"
            "lbLabelFontHeightF" : 0.014
            "lbLabelStride" : 1
            "lbTitleOn" : True
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleString" : "m:S:2:N: s:S:-2:N:"
            "lbTitleFontHeightF" : 0.016
    end setvalues

    draw((/mp,cn_sreh/))
    setvalues mp
            "mpFillOn" : False
    end setvalues
    draw(mp)
    draw((/date_time,level_anno/))

    frame(wks)

    ;---------------------------- Theta-E Plot -----------------------------

    print ("Plotting Theta-E contours")
    setvalues cn_thet
            "vpXF" : vpx
            "vpYF" : vpy
            "vpWidthF" : vpw
            "vpHeightF" : vph
            "tiMainString" : thet_string
            "tiMainFontHeightF" : 0.025
            "pmLabelBarDisplayMode" : "ALWAYS"
            "pmLabelBarHeightF"            : 0.05
            "pmLabelBarWidthF"             : vpw*0.75
            "pmLabelBarParallelPosF"       : 0.995
            "pmLabelBarOrthogonalPosF"     : 0.0
            "lbLabelsOn" : "True"
            "lbLabelStride" : 2
            "lbLabelFontHeightF" : 0.014
            "lbTitleOn" : True
            "lbTitlePosition" : "Bottom"
            "lbTitleJust" : "BottomRight"
            "lbTitleString" : ":S:o:N:C"
            "lbTitleFontHeightF" : 0.016
    end setvalues
    draw((/cn_thet,mp/))
    draw((/date_time,level_anno/))

    frame(wks)

    ;---------------------------- Precip Plot ------------------------------

    if (precip_flag) then
        print ("Plotting Precip contours")
        setvalues mp
                "mpFillOn" : True
        end setvalues
        setvalues cn_pcpt
                "vpXF" : vpx
                "vpYF" : vpy
                "vpWidthF" : vpw
                "vpHeightF" : vph
                "tiMainString" : prcp_string
                "tiMainFontHeightF" : 0.025
                "pmLabelBarDisplayMode" : "ALWAYS"
                "pmLabelBarHeightF"            : 0.05
                "pmLabelBarWidthF"             : vpw*0.75
                "pmLabelBarParallelPosF"       : 0.995
                "pmLabelBarOrthogonalPosF"     : 0.0
                "lbLabelFontHeightF" : 0.014
                "lbLabelsOn" : "True"
                "lbLabelStride" : 1
                "lbTitleOn" : True
                "lbTitlePosition" : "Bottom"
                "lbTitleJust" : "BottomRight"
                "lbTitleString" : "inches"
                "lbTitleFontHeightF" : 0.016
        end setvalues
        setvalues cn_pcp1
                "vpXF" : vpx
                "vpYF" : vpy
                "vpWidthF" : vpw
                "vpHeightF" : vph
                "cnFillOn" : "False"
                "cnLinesOn" : "True"
                "pmLabelBarDisplayMode" : "NEVER"
        end setvalues
        setvalues cn_pcp2
                "vpXF" : vpx
                "vpYF" : vpy
                "vpWidthF" : vpw
                "vpHeightF" : vph
                "cnFillOn" : "False"
                "cnLinesOn" : "True"
                "cnLineDashPattern" : 2
                "cnLineDashSegLenF" : .10
                "pmLabelBarDisplayMode" : "NEVER"
        end setvalues

        draw((/mp,cn_pcpt,cn_pcp1,cn_pcp2/))
        setvalues mp
                "mpFillOn" : False
        end setvalues
        draw(mp)
        draw((/date_time,level_anno/))

        frame(wks)

    end if

end
